subprojects {
    apply plugin: "java"

    version = "1.0"
    sourceCompatibility = "1.8"

    repositories {
        mavenCentral()
    }
}

project("dataapp-library") {
    dependencies {
        compileOnly "org.eclipse.persistence:javax.persistence:2.1.0"
    }
}

project("dataapp-server") {
    apply plugin: "war"
    apply from: "https://raw.github.com/akhikhl/gretty/master/pluginScripts/gretty.plugin"

    dependencies {
        compile project(":dataapp-library")
        compileOnly 'javax.servlet:javax.servlet-api:3.1.0'
        compile 'org.hibernate:hibernate-core:5.2.4.Final'
        compile 'org.glassfish.jersey.containers:jersey-container-servlet:2.24.1'
        compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.24.1'
        compile 'org.quartz-scheduler:quartz:2.2.1'
        runtime 'com.h2database:h2:1.4.193'
    }

    gretty {
        scanInterval = 0
    }
}

project("dataapp-client") {
    dependencies {
        compile project(":dataapp-preloader")
        compile project(":dataapp-library")
        compile "javax.ws.rs:javax.ws.rs-api:2.0.1"
        compile "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.8.4"
        runtime "org.glassfish.jersey.core:jersey-client:2.24"
    }

    jar {
        manifest {
            attributes(
                    "Permissions": "sandbox",
                    "JavaFX-Version": "8.0",
                    "JavaFX-Application-Class": "com.javafx.experiments.dataapp.client.DataApplication",
                    "JavaFX-Preloader-Class": "dataapppreloader.DataAppPreloader",
                    "JavaFX-Class-Path": configurations.runtime.collect { it.name }.join(" "),
                    "Main-Class": "com.javafx.experiments.dataapp.client.DataApplication"
            )
        }
    }

    task copyDeps(type: Copy) {
        from configurations.runtime
        into libsDir

        shouldRunAfter jar
    }

    assemble.dependsOn += copyDeps
}